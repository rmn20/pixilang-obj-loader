include "obj_loader.pixi"

fn gl_callback($userdata)
{
    set_screen(GL_SCREEN) //Enable OpenGL drawing mode
    set_zbuf(GL_ZBUF)

    clear(#203040)
    clear_zbuf()

    t_set_camera(
        player[0], player[1] + 170, player[2],
        player.rotX, player.rotY, 0,
        1, 
        20000,
        100
    )

    gl_draw_arrays( GL_TRIANGLES, 0, obj.size, 255, 255, 255, 255, tex, obj, obj.cols, obj.uvs )

    t_reset()

    ts = ""
    sprintf(ts, "FPS:%u", FPS)
    print(ts, -WINDOW_XSIZE / 2 + 8, -WINDOW_YSIZE / 2 + 8, WHITE, TOP | LEFT)

    set_zbuf(-1)
    set_screen(0) //Back to the default screen
}

start_timer( 0 )

tex = -1
obj = obj_load( "test2.obj", 1 ) ; tex = load( "tex2.png" )
//obj = obj_load( "test_cube.obj" ) ; tex = load( "tex2.png" )

player = new(3, 1, FLOAT)
clean(player)

player.rotX = 0
player.rotY = 0

keyboard = new(256, 1, INT8)
clean(keyboard)

set_gl_callback(gl_callback, 0) 

while 1
{
    while(get_event()) {
		if EVT[EVT_TYPE] == EVT_QUIT {halt}
		
		if (EVT[EVT_TYPE] == EVT_BUTTONDOWN) || (EVT[EVT_TYPE] == EVT_BUTTONUP) {
			if EVT[EVT_KEY] < 256 {keyboard[EVT[EVT_KEY]] = EVT[EVT_TYPE] == EVT_BUTTONDOWN}
		}
	}
	
	//Player input
	if keyboard[KEY_LEFT] {player.rotY + 3}
	if keyboard[KEY_RIGHT] {player.rotY - 3}
	if keyboard[KEY_UP] {player.rotX + 3}
	if keyboard[KEY_DOWN] {player.rotX - 3}
	
	$front = 0
	if keyboard['w'] {$front + 1}
	if keyboard['s'] {$front - 1}
	$right = 0
	if keyboard['d'] {$right + 1}
	if keyboard['a'] {$right - 1}
	
	walkEntity(player, $front * 1.6, $right * 1.6)
	
    frame()
}

remove(player)
remove(keyboard)
obj_remove(obj)

fn walkEntity($entity, $front, $right) {
	$sinv = sin($entity.rotY * M_PI / 180)
	$cosv = cos($entity.rotY * M_PI / 180)
	
	if($front != 0 && $right != 0) {
		//1 / (2^0.5) ~= 0.70710678
		$front * 0.70710678
		$right * 0.70710678
	}
	
	$entity[0] + (-$sinv * $front + $cosv * $right)
	$entity[2] + (-$cosv * $front - $sinv * $right)
}

//x,y,z - camera position;
//r1 - rotation around the X-axis;
//r2 - rotation around the Y-axis;
//r3 - rotation around the Z-axis;
//near,far - visible Z range;
//angle_of_view - viewing angle in degrees
fn t_set_camera( $x, $y, $z, $r1, $r2, $r3, $near, $far, $angle_of_view )
{
    $aspect_ratio = WINDOW_XSIZE / WINDOW_YSIZE
    $scale = tan( $angle_of_view * 0.5 * M_PI / 180 ) * $near
    $r = $aspect_ratio * $scale
    $l = -$r
    $t = $scale
    $b = -$t
    t_frustum( $l, $r, $b, $t, $near, $far )
    if $r1 { t_rotate( $r1, -1, 0, 0 ) }
    if $r2 { t_rotate( $r2, 0, -1, 0 ) }
    if $r3 { t_rotate( $r3, 0, 0, 1 ) }
    t_translate( -$x, -$y, -$z )
}

//Parallel projection:
fn t_orthographic( $left, $right, $bottom, $top, $z_near, $z_far )
{
    t_scale( WINDOW_XSIZE / 2, -WINDOW_YSIZE / 2, -WINDOW_ZSIZE / 2 ) //OpenGL NDC -> Pixilang coordinates

    if tmp_matrix == 0 { tmp_matrix = new( 4, 4, FLOAT ) }
    $m = tmp_matrix
    clean( $m )
    $m[ 0 ] = 2 / ( $right - $left )
    $m[ 4 + 1 ] = 2 / ( $top - $bottom )
    $m[ 8 + 2 ] = -2 / ( $z_far - $z_near )
    $m[ 12 + 0 ] = - ( $right + $left ) / ( $right - $left )
    $m[ 12 + 1 ] = - ( $top + $bottom ) / ( $top - $bottom )
    $m[ 12 + 2 ] = - ( $z_far + $z_near ) / ( $z_far - $z_near )
    $m[ 12 + 3 ] = 1
    t_mul_matrix( $m )
}

//Perspective projection:
fn t_frustum( $left, $right, $bottom, $top, $z_near, $z_far )
{
    t_scale( WINDOW_XSIZE / 2, -WINDOW_YSIZE / 2, -WINDOW_ZSIZE / 2 ) //OpenGL NDC -> Pixilang coordinates

    if tmp_matrix == 0 { tmp_matrix = new( 4, 4, FLOAT ) }
    $m = tmp_matrix
    clean( $m )
    $m[ 0 ] = 2 * $z_near / ( $right - $left )
    $m[ 5 ] = 2 * $z_near / ( $top - $bottom )
    $m[ 8 ] = ( $right + $left ) / ( $right - $left )
    $m[ 9 ] = ( $top + $bottom ) / ( $top - $bottom )
    $m[ 10 ] = -( $z_far + $z_near ) / ( $z_far - $z_near )
    $m[ 11 ] = -1
    $m[ 14 ] = -2 * $z_far * $z_near / ( $z_far - $z_near )
    t_mul_matrix( $m )
}
